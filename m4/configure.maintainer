dnl -*- mode: Autoconf; -*-

dnl ----------------------------------------------------------------------------
dnl --SECTION--                                                  MAINTAINER MODE
dnl ----------------------------------------------------------------------------

AC_ARG_ENABLE(maintainer-mode,
  AS_HELP_STRING([--enable-maintainer-mode], [enable maintainer mode (default: no)]),
  [tr_MAINTAINER_MODE="${enableval:-yes}"],
  [tr_MAINTAINER_MODE=no]
)

if test "x$tr_MAINTAINER_MODE" = "xyes";  then
  AC_DEFINE_UNQUOTED(TRI_ENABLE_MAINTAINER_MODE, 1, [true if maintainer mode is enabled])
fi

AM_CONDITIONAL(ENABLE_MAINTAINER_MODE, test "x$tr_MAINTAINER_MODE" = xyes)

dnl ----------------------------------------------------------------------------
dnl check for bison
dnl ----------------------------------------------------------------------------

if test "x$tr_MAINTAINER_MODE" = xyes;  then
  AC_MSG_NOTICE([................................................................................])
  AC_MSG_NOTICE([CHECKING BISON])
  AC_MSG_NOTICE([................................................................................])

  AC_CHECK_PROG(BISON, bison, bison)

  tr_bison_usable=no

  if test "x$BISON" != x -a "x$BISON" != "x:"; then
    AC_MSG_CHECKING([if bison supports required features])

    [tr_bison_version=`$BISON --version | head -1 | sed -e 's:^[^0-9]*::' | awk -F. '{print $1 * 10000 + $2 * 100 + $3}'`] 

    if test "$tr_bison_version" -ge 20401; then
      tr_bison_usable=yes
    fi

    AC_MSG_RESULT($tr_bison_usable)
  fi

  if test "x$tr_bison_usable" != "xyes"; then
    AC_MSG_ERROR([Please install bison version 2.4.1 or higher from http://ftp.gnu.org/gnu/bison/])
  fi

  AC_MSG_CHECKING([BISON version])
  TRI_BISON_VERSION=`$BISON --version | head -1`
  AC_MSG_RESULT([$TRI_BISON_VERSION])

  BASIC_INFO="$BASIC_INFO|BISON: ${TRI_BISON_VERSION}"
else
  BASIC_INFO="$BASIC_INFO|BISON: disabled"
fi

dnl ----------------------------------------------------------------------------
dnl check for flex
dnl ----------------------------------------------------------------------------

if test "x$tr_MAINTAINER_MODE" = xyes;  then
  AC_MSG_NOTICE([................................................................................])
  AC_MSG_NOTICE([CHECKING FLEX])
  AC_MSG_NOTICE([................................................................................])

  AC_PROG_LEX

  tr_flex_usable=no

  if test "x$LEX" != x -a "x$LEX" != "x:";  then
    AC_MSG_CHECKING([if flex supports required features])

    [tr_flex_version=`$LEX --version | sed -e 's:^[^0-9]*::' | awk -F. '{print $1 * 10000 + $2 * 100 + $3}'`]

    if test "$tr_flex_version" -ge 20535;  then
      tr_flex_usable=yes
    fi

    AC_MSG_RESULT($tr_flex_usable)
  fi

  if test "x$tr_flex_usable" != "xyes"; then
    AC_MSG_ERROR([Please install flex version 2.5.35 or higher from http://ftp.gnu.org/gnu/flex/])
  fi

  AC_MSG_CHECKING([FLEX version])
  TRI_FLEX_VERSION=`$LEX --version`
  AC_MSG_RESULT([$TRI_FLEX_VERSION])

  BASIC_INFO="$BASIC_INFO|FLEX: ${TRI_FLEX_VERSION}"
else
  BASIC_INFO="$BASIC_INFO|FLEX: disabled"
fi

dnl ----------------------------------------------------------------------------
dnl --SECTION--                                                      END-OF-FILE
dnl ----------------------------------------------------------------------------

dnl Local Variables:
dnl mode: outline-minor
dnl outline-regexp: "^\\(### @brief\\|## --SECTION--\\|# -\\*- \\)"
dnl End:
